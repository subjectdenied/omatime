import QtQuick
import qs.Commons
import qs.Ui

BarWidget {
  id: root
  moduleName: "chris.timetracker"

  function injectPanel() {
    var target = panelLoader.item
    if (!target) return
    if ("bar" in target) target.bar = root.bar
    if ("settings" in target) target.settings = root.settings
    if ("anchorItem" in target) target.anchorItem = button
    if ("hostWidget" in target) target.hostWidget = root
  }

  function togglePanel() {
    if (panelLoader.item && panelLoader.item.toggle) panelLoader.item.toggle()
  }

  // Shape contract for shell.summon/hide/toggle routing (see weather widget).
  readonly property bool opened: panelLoader.item ? panelLoader.item.opened === true : false

  function open() {
    if (panelLoader.item && panelLoader.item.openFromHotkey) panelLoader.item.openFromHotkey()
  }

  function close() {
    if (panelLoader.item && panelLoader.item.close) panelLoader.item.close()
  }

  readonly property bool popoutSwitchClosing: panelLoader.item ? panelLoader.item.popoutSwitchClosing === true : false

  function closeForPopoutSwitch() {
    if (panelLoader.item) panelLoader.item.closeForPopoutSwitch()
  }

  implicitWidth: button.implicitWidth
  implicitHeight: button.implicitHeight

  onBarChanged: injectPanel()
  onSettingsChanged: injectPanel()

  Loader {
    id: panelLoader
    active: true
    source: Qt.resolvedUrl("Panel.qml")
    visible: false
    onLoaded: {
      root.injectPanel()
      Qt.callLater(root.injectPanel)
    }
  }

  // WidgetButton statt BarIconButton: der Icon-Slot hat fixe Breite und das
  // Label ("󱎫 26:21") würde über die Nachbar-Widgets malen; WidgetButton
  // nimmt seine natürliche Textbreite.
  WidgetButton {
    id: button
    anchors.fill: parent
    bar: root.bar
    text: panelLoader.item ? panelLoader.item.label : "󱎫"

    onPressed: function(b) {
      if (!root.bar) return
      if (b === Qt.MiddleButton && panelLoader.item) {
        // Middle click: start/stop without opening the panel.
        if (panelLoader.item.runningEntry) panelLoader.item.stopTimer()
        else panelLoader.item.startTimer(null)
      } else {
        root.togglePanel()
      }
    }
  }
}
